allprojects  {
  apply plugin: 'maven-publish'
  group = 'org.apache.yoko'
  version = '1.5-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'osgi'
  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  repositories {
    mavenLocal()

    maven { url "http://repository.apache.org/snapshots" }
    maven { url "http://repo.maven.apache.org/maven2" }
  }

  configurations {
    // declare a new configuration to map to Maven's provided scope
    provided.transitive = false
  }

  dependencies {}

  sourceSets.main.compileClasspath += configurations.provided

  publishing {
    publications {
      maven(MavenPublication) {
        from components.java
        artifact jar

        pom.withXml {
          def root = asNode()

          // specify packaging type as bundle (default is pom)
          root.packaging[0].setValue('bundle')

          def deps = root.dependencies[0] ?:
                     root.appendNode('dependencies')

          // declare 'provided' scope dependencies in the pom.xml
          // Note: the 'provided' scope is built into Maven,
          // but it is a custom configuration in this Gradle
          configurations.provided.allDependencies.each {
            def dep = deps.appendNode('dependency')
            dep.appendNode('groupId', it.group)
            dep.appendNode('artifactId', it.name)
            dep.appendNode('version', it.version)
            dep.appendNode('scope', 'provided')
          }
        }
      }
    }
  }

  jar {
    dependsOn "generatePomFileForMavenPublication"
    // generate the maven dependency metadata
    into("/META-INF/maven/$project.group/$project.name") {
      from 'build/publications/maven'
       rename "pom-default.xml", "pom.xml"
    }
    manifest {
      instruction 'Bundle-DocURL'          , "http://geronimo.apache.org/maven/yoko/$project.version/$project.name"
      instruction 'Bundle-License'         , 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      instruction 'Bundle-Vendor'          , 'The Apache Software Foundation'
      instruction 'Import-Package'         ,  '*'
    }
  }
}
